buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('org.testcontainers:postgresql:1.18.1')
    }
}

import org.testcontainers.containers.PostgreSQLContainer

plugins {
    id 'org.springframework.boot' version '3.1.4'
    id 'org.flywaydb.flyway' version '9.7.0'
    id 'nu.studer.jooq' version '8.2'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    implementation project(':components:common')
    implementation project(':components:consumer-common')
    implementation project(':components:scheduler')

    implementation(libs.yaci.store.starter)

    implementation(libs.cardano.client.lib)

    implementation(libs.snakeyaml)
    implementation(libs.guava)
    implementation(libs.log4j.core)
    implementation(libs.log4j.api)
    implementation(libs.map.struct)

    runtimeOnly 'org.postgresql:postgresql'

    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)
    annotationProcessor(libs.lombok.mapstruct.binding)
    annotationProcessor(libs.mapstruct.processor)

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation('org.hsqldb:hsqldb')
    testImplementation(libs.pitest)
    testCompileOnly(libs.lombok)
    testAnnotationProcessor(libs.lombok)
}

compileJava {
    options.compilerArgs += ['-Amapstruct.defaultComponentModel=spring']
}

configurations {
    flywayMigration
}

dependencies {
    flywayMigration 'org.postgresql:postgresql'
    jooqGenerator 'org.postgresql:postgresql'
}

ext {
    dbUrl = ''
    dbUser = ''
    dbPassword = ''
    dbSchema = 'mainnet'
    instance = null
}

task postgresqlContainer {
    def postgresContainer = new PostgreSQLContainer<>("postgres:14.5")
            .withDatabaseName('ledger_sync')
    postgresContainer.start()
    dbUrl = postgresContainer.getJdbcUrl()
    dbUser = postgresContainer.getUsername()
    dbPassword = postgresContainer.getPassword()
    instance = postgresContainer
}

flyway {
    configurations = ['flywayMigration']
    url = dbUrl
    user = dbUser
    password = dbPassword
    locations = ['filesystem:src/main/resources/db/migration/consumer']
    createSchemas = true
    schemas = [dbSchema]
}

jooq {
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = dbUrl
                    user = dbUser
                    password = dbPassword
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'

                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = "${dbSchema}.*"
                        excludes = 'FLYWAY_SCHEMA_HISTORY | UNUSED_TABLE | PREFIX_.* | SECRET_SCHEMA.SECRET_TABLE | SECRET_ROUTINE'
                        schemata {
                            schema {
                                inputSchema = dbSchema
                                outputSchemaToDefault = true
                            }
                        }
                    }
                    target {
                        directory = file('src/generated')
                        packageName = 'org.cardanofoundation.ledgersync.jooq';
                    }
                    generate {
                        // Generate the DAO classes
                        daos = true
                        // Annotate DAOs (and other types) with spring annotations, such as @Repository and @Autowired
                        // for auto-wiring the Configuration instance, e.g. from Spring Boot's jOOQ starter
                        springAnnotations = true
                        // Generate Spring-specific DAOs containing @Transactional annotations
                        springDao = true
                    }
                }
            }
        }
    }
}

tasks.named('generateJooq').configure {
    dependsOn tasks.named('postgresqlContainer')
    dependsOn tasks.named('flywayMigrate')

    inputs.files(fileTree('src/main/resources/db/migration/consumer'))
            .withPropertyName('migrations')
            .withPathSensitivity(PathSensitivity.RELATIVE)

    allInputsDeclared = true

    doLast {
        // stop container
        instance.stop()
    }
}

jar {
    enabled = false
}
